dnl # ========================================================================= #
dnl #                                                                           #
dnl #                 The Apache Software License,  Version 1.1                 #
dnl #                                                                           #
dnl #          Copyright (c) 1999-2001 The Apache Software Foundation.          #
dnl #                           All rights reserved.                            #
dnl #                                                                           #
dnl # ========================================================================= #
dnl #                                                                           #
dnl # Redistribution and use in source and binary forms,  with or without modi- #
dnl # fication, are permitted provided that the following conditions are met:   #
dnl #                                                                           #
dnl # 1. Redistributions of source code  must retain the above copyright notice #
dnl #    notice, this list of conditions and the following disclaimer.          #
dnl #                                                                           #
dnl # 2. Redistributions  in binary  form  must  reproduce the  above copyright #
dnl #    notice,  this list of conditions  and the following  disclaimer in the #
dnl #    documentation and/or other materials provided with the distribution.   #
dnl #                                                                           #
dnl # 3. The end-user documentation  included with the redistribution,  if any, #
dnl #    must include the following acknowlegement:                             #
dnl #                                                                           #
dnl #       "This product includes  software developed  by the Apache  Software #
dnl #        Foundation <http://www.apache.org/>."                              #
dnl #                                                                           #
dnl #    Alternately, this acknowlegement may appear in the software itself, if #
dnl #    and wherever such third-party acknowlegements normally appear.         #
dnl #                                                                           #
dnl # 4. The names  "The  Jakarta  Project",  "WebApp",  and  "Apache  Software #
dnl #    Foundation"  must not be used  to endorse or promote  products derived #
dnl #    from this  software without  prior  written  permission.  For  written #
dnl #    permission, please contact <apache@apache.org>.                        #
dnl #                                                                           #
dnl # 5. Products derived from this software may not be called "Apache" nor may #
dnl #    "Apache" appear in their names without prior written permission of the #
dnl #    Apache Software Foundation.                                            #
dnl #                                                                           #
dnl # THIS SOFTWARE IS PROVIDED "AS IS" AND ANY EXPRESSED OR IMPLIED WARRANTIES #
dnl # INCLUDING, BUT NOT LIMITED TO,  THE IMPLIED WARRANTIES OF MERCHANTABILITY #
dnl # AND FITNESS FOR  A PARTICULAR PURPOSE  ARE DISCLAIMED.  IN NO EVENT SHALL #
dnl # THE APACHE  SOFTWARE  FOUNDATION OR  ITS CONTRIBUTORS  BE LIABLE  FOR ANY #
dnl # DIRECT,  INDIRECT,   INCIDENTAL,  SPECIAL,  EXEMPLARY,  OR  CONSEQUENTIAL #
dnl # DAMAGES (INCLUDING,  BUT NOT LIMITED TO,  PROCUREMENT OF SUBSTITUTE GOODS #
dnl # OR SERVICES;  LOSS OF USE,  DATA,  OR PROFITS;  OR BUSINESS INTERRUPTION) #
dnl # HOWEVER CAUSED AND  ON ANY  THEORY  OF  LIABILITY,  WHETHER IN  CONTRACT, #
dnl # STRICT LIABILITY, OR TORT  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN #
dnl # ANY  WAY  OUT OF  THE  USE OF  THIS  SOFTWARE,  EVEN  IF  ADVISED  OF THE #
dnl # POSSIBILITY OF SUCH DAMAGE.                                               #
dnl #                                                                           #
dnl # ========================================================================= #
dnl #                                                                           #
dnl # This software  consists of voluntary  contributions made  by many indivi- #
dnl # duals on behalf of the  Apache Software Foundation.  For more information #
dnl # on the Apache Software Foundation, please see <http://www.apache.org/>.   #
dnl #                                                                           #
dnl # ========================================================================= #

dnl -----------------------------------------------------------------------------
dnl Author  Pier Fumagalli <mailto:pier.fumagalli@eng.sun.com>
dnl Author Jon S. Stevens <mailto:jon@latchkey.com>
dnl Version $Id$
dnl -----------------------------------------------------------------------------

dnl -----------------------------------------------------------------------------
dnl Initialize GNU AutoConf
dnl -----------------------------------------------------------------------------
AC_INIT(Makefile.in)

dnl -----------------------------------------------------------------------------
dnl Define the local functions we're going to use
dnl -----------------------------------------------------------------------------
AC_DEFUN(LOCAL_HEADER,[
    printf "\n\033\13301;31m%s\033\13300m\n" "$1" 1>&2
    AC_PROVIDE([$0])
])

AC_DEFUN(LOCAL_FILTEREXEC,[{
    $1
    echo RETVAL $?
}|{
    RET=0
    while ${TRUE}
    do
        read FIRST LINE
        if ${TEST} ! "$?" -eq "0"
        then
            break
        else
            if ${TEST} "$FIRST" = "RETVAL"
            then
                RET="$LINE"
            else
                printf "\033\13301m%s\033\13300m: %s\n" "$2" "$FIRST $LINE" 1>&2
            fi
        fi
    done
    unset FIRST
    unset LINE
}])

dnl -----------------------------------------------------------------------------
dnl Check out ranlib and ar
dnl -----------------------------------------------------------------------------
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar,${PATH})
AC_PATH_PROG(TEST,test,$PATH)
AC_PATH_PROG(TRUE,true,$PATH)
AC_SUBST(AR)
AC_SUBST(TEST)
AC_SUBST(TRUE)

dnl -----------------------------------------------------------------------------
dnl Process the --with-apxs[=FILE]... command line argument
dnl -----------------------------------------------------------------------------
AC_ARG_WITH(apxs,
[  --with-apxs[=FILE]      Build shared Apache module. FILE is the optional 
                          pathname to the apxs tool; defaults to finding 
                          apxs in your PATH.],
[
    case "${withval}" in 
        y | yes | true) find_apxs=true ;;
        n | no | false) find_apxs=false ;;
        *) find_apxs=true ;;
    esac

    if ${TEST} ${find_apxs} ; then    
        AC_MSG_RESULT([need to check for Perl first, apxs depends on it...])
        AC_PATH_PROG(PERL,perl,$PATH)dnl
   
        dnl if the value is an excutable otherwise try the PATH.
        if ${TEST} ! -x ${withval} ; then
            AC_PATH_PROG(APXS,apxs,$PATH)dnl
        else
            APXS=${withval}
        fi
    
        if ${TEST} -n "${APXS}" ; then
            dnl Seems that we have it, but have to check if it is OK first        
            if ${TEST} ! -x "${APXS}" ; then
                AC_MSG_ERROR(Invalid location for apxs: '${APXS}')
            fi
            
            $APXS -q PREFIX >/dev/null 2>/dev/null || apxs_support=false
    
            if ${TEST} "${apxs_support}" = "false" ; then
                AC_MSG_RESULT(could not find apxs)
                AC_MSG_ERROR(You must specify a valid --with-apxs path)
            fi
            
            TGTDIRS="$TGTDIRS apache-1.3"
            AC_MSG_RESULT([adding apache-1.3 to target directories: \"$TGTDIRS\"])
    
            AC_SUBST(TGTDIRS)
            AC_SUBST(APXS)
        fi
    fi
],
[
	AC_MSG_RESULT(no)
])

dnl -----------------------------------------------------------------------------
dnl Check where sources are
dnl -----------------------------------------------------------------------------
AC_MSG_CHECKING([sources directory])
SRCDIR=$srcdir
CURDIR=`pwd`
cd $SRCDIR
SRCDIR=`pwd`
cd $CURDIR
AC_MSG_RESULT($SRCDIR)
AC_SUBST(SRCDIR)

dnl -----------------------------------------------------------------------------
dnl Check where targets are
dnl -----------------------------------------------------------------------------
AC_MSG_CHECKING([targets directory])
TGTDIR=`pwd`
AC_MSG_RESULT($TGTDIR)
AC_SUBST(TGTDIR)

dnl -----------------------------------------------------------------------------
dnl Process the --with-apr=... command line argument (required)
dnl -----------------------------------------------------------------------------
AC_MSG_CHECKING([APR directory])
AC_ARG_WITH(apr,
  [  --with-apr=DIR          where the APR sources can be found],
  APRDIR=${withval},
  APRDIR="${srcdir}/apr"
)

if ${TEST} ! -d "${APRDIR}" ; then
  AC_MSG_ERROR([Cannot find APR sources directory \"${APRDIR}\"])
fi

if ${TEST} ! -x "${APRDIR}/buildconf" ; then
  AC_MSG_ERROR([Cannot find APR buildconf program in \"${APRDIR}\"])
fi

CURDIR=`pwd`
cd ${APRDIR}
APRDIR=`pwd`
AC_MSG_RESULT([${APRDIR}])
cd ${CURDIR}
AC_SUBST(APRDIR)

dnl -----------------------------------------------------------------------------
dnl Build the configure script for APR and run it
dnl -----------------------------------------------------------------------------
cd ${APRDIR}

LOCAL_HEADER([Building APR configure script])
LOCAL_FILTEREXEC(./buildconf,[  APR buildconf])
if ${TEST} "${RET}" -ne "0"
then
  AC_MSG_ERROR([APR buildconf terminated with error code ${RET}])
fi

LOCAL_HEADER([Configuring APR])
LOCAL_FILTEREXEC(./configure,[  APR configure])
if ${TEST} "${RET}" -ne "0"
then
  AC_MSG_ERROR([APR configure script terminated with error code ${RET}])
fi

LOCAL_HEADER([Done with APR configuration])
cd ${CURDIR}

AC_OUTPUT(Makedefs Makefile lib/Makefile apache-1.3/Makefile)
